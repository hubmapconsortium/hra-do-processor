PREFIX ccf: <http://purl.org/ccf/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT DISTINCT
  ?node_id_str
  ?node_group_str
  ?node_label_str
  ?node_mapped_to_str
  ?ftu_illustration
  ?tissue_label_str
  ?tissue_mapped_to_str
  ?organ_label_str
  ?organ_mapped_to_str
WHERE {
  ?ftu_illustration_node a ccf:FtuIllustrationNode ;
      ccf:node_name ?node_id ;
      ccf:node_group ?node_group ;
      ccf:representation_of ?biological_concept ;
      ccf:part_of_illustration ?ftu_illustration .

  ?biological_concept rdfs:label ?node_label .

  ?ftu_illustration ccf:representation_of ?organ_concept .
  ?ftu_illustration ccf:ccf_located_in ?parent_organ_concept .

  ?organ_concept rdfs:label ?tissue_label .
  ?organ_concept oboInOwl:id ?tissue_mapped_to .

  ?parent_organ_concept rdfs:label ?organ_label .
  ?parent_organ_concept oboInOwl:id ?organ_mapped_to .

  # Implement BIND(STR()) to convert results to strings
  BIND(STR(?node_id) AS ?node_id_str)
  BIND(STR(?node_group) AS ?node_group_str)
  BIND(STR(?node_label) AS ?node_label_str)
  BIND(STR(?biological_concept) AS ?node_mapped_to_str)
  BIND(STR(?tissue_label) AS ?tissue_label_str)
  BIND(STR(?tissue_mapped_to) AS ?tissue_mapped_to_str)
  BIND(STR(?organ_label) AS ?organ_label_str)
  BIND(STR(?organ_mapped_to) AS ?organ_mapped_to_str)
}