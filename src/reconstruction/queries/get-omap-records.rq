PREFIX ccf: <http://purl.org/ccf/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT DISTINCT 
    ?experiment
    ?study_method ?tissue_preservation
    (GROUP_CONCAT(DISTINCT ?protocol_doi; separator=", ") AS ?protocol_doi_str)
    (GROUP_CONCAT(DISTINCT ?author_orcid; separator=", ") AS ?author_orcid_str)
    ?sample_organ ?sample_organ_label
    ?cycle ?cycle_number ?used_antibody
    ?antibody_manufacturer ?antibody_type
    ?concentration ?dilution ?is_core_panel
    ?registered_antibody ?clone_id ?lot_number 
    ?catalog_number ?host ?isotype 
    ?clonality ?conjugate ?recombinant 
    ?record_number ?antibody 
    (GROUP_CONCAT(DISTINCT ?detected_protein_id; separator=", ") AS ?detected_protein_id_str) 
    (GROUP_CONCAT(DISTINCT ?detected_protein_label; separator=", ") AS ?detected_protein_label_str)
    (GROUP_CONCAT(DISTINCT ?uniprot_id; separator=", ") AS ?uniprot_id_str)
    ?detects_rationale ?binds_to_rationale
WHERE {
    # Get MultiplexedAntibodyBasedImagingExperiment instances
    ?experiment rdf:type ccf:MultiplexedAntibodyBasedImagingExperiment .
    
    # Experiment properties
    ?experiment ccf:study_method ?study_method .
    ?experiment ccf:tissue_preservation ?tissue_preservation .
    OPTIONAL { ?experiment ccf:protocol_doi ?protocol_doi . }
    OPTIONAL { ?experiment ccf:author_orcid ?author_orcid . }
    
    # Sample organ information
    ?experiment ccf:sample_organ ?sample_organ .
    OPTIONAL { ?sample_organ rdfs:label ?sample_organ_label . }
    
    # Experiment cycles
    ?experiment ccf:has_cycle ?cycle .
    ?cycle rdf:type ccf:ExperimentCycle .
    OPTIONAL { ?cycle ccf:cycle_number ?cycle_number . }
    
    # Antibodies used in cycles
    OPTIONAL {
      ?cycle ccf:uses_antibody ?used_antibody .
      ?used_antibody rdf:type ccf:ExperimentUsedAntibody .
      OPTIONAL { ?used_antibody ccf:concentration ?concentration . }
      OPTIONAL { ?used_antibody ccf:dilution ?dilution . }
      OPTIONAL { ?used_antibody ccf:is_core_panel ?is_core_panel . }
      OPTIONAL { ?used_antibody ccf:record_number ?record_number . }

      OPTIONAL {
        ?used_antibody ccf:detects ?detected_protein_id .

        # Protein detection information
        OPTIONAL {
          ?detects_statement rdf:type owl:Axiom .
          ?detects_statement owl:annotatedSource ?used_antibody .
          ?detects_statement owl:annotatedTarget ?detected_protein_id .
          ?detects_statement owl:annotatedProperty ccf:detects .
          ?detects_statement ccf:rationale ?detects_rationale .
        }

        ?detected_protein_id rdfs:label ?detected_protein_label .
        ?detected_protein_id oboInOwl:hasDbXref ?uniprot_id .

        # UniProt information - mandatory filtering for UniProt IDs only
        ?hasDbXref_statement rdf:type owl:Axiom .
        ?hasDbXref_statement owl:annotatedSource ?detected_protein_id .
        ?hasDbXref_statement owl:annotatedTarget ?uniprot_id .
        ?hasDbXref_statement owl:annotatedProperty oboInOwl:hasDbXref .
        ?hasDbXref_statement rdfs:comment "UniProt ID"^^xsd:string.
      }

      OPTIONAL {
        ?used_antibody ccf:binds_to ?bound_protein_id .

        # Protein binding information with rationale
        OPTIONAL {
          ?binds_to_statement rdf:type owl:Axiom .
          ?binds_to_statement owl:annotatedSource ?used_antibody .
          ?binds_to_statement owl:annotatedTarget ?bound_protein_id .
          ?binds_to_statement owl:annotatedProperty ccf:binds_to .
          ?binds_to_statement ccf:rationale ?binds_to_rationale .
        }
      }

      # Registered antibody details
      OPTIONAL {
        ?used_antibody ccf:based_on ?registered_antibody .
        ?registered_antibody rdf:type ?antibody .
        FILTER(?antibody != owl:NamedIndividual)
        OPTIONAL { ?registered_antibody ccf:lot_number ?lot_number . }
        OPTIONAL { ?registered_antibody ccf:isotype ?isotype . }
        OPTIONAL { ?antibody ccf:antibody_manufacturer ?antibody_manufacturer . }
        OPTIONAL { ?antibody ccf:antibody_type ?antibody_type . }
        OPTIONAL { ?antibody ccf:catalog_number ?catalog_number . }
        OPTIONAL { ?antibody ccf:host ?host . }
        OPTIONAL { ?antibody ccf:recombinant ?recombinant . }
        OPTIONAL { ?antibody ccf:clone_id ?clone_id . }
        OPTIONAL { ?antibody ccf:clonality ?clonality . }
        OPTIONAL { ?antibody ccf:conjugate ?conjugate . }
      }
    }
}
GROUP BY ?experiment ?study_method ?tissue_preservation ?sample_organ ?sample_organ_label ?cycle ?cycle_number ?used_antibody ?antibody_manufacturer ?antibody_type ?concentration ?dilution ?is_core_panel ?record_number ?registered_antibody ?clone_id ?lot_number ?catalog_number ?host ?isotype ?clonality ?conjugate ?recombinant ?antibody ?detected_protein_id_str ?detected_protein_label_str ?uniprot_id_str ?detects_rationale ?binds_to_rationale
ORDER BY ?record_number ?cycle_number
